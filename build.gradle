buildscript {
    ext {
        kotlinVersion = '1.2.31'
        jacksonVersion = '2.9.4'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlinVersion
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-allopen', version: kotlinVersion
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-noarg', version: kotlinVersion

        classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.9.0'
        classpath group: 'io.codearte.gradle.nexus', name: 'gradle-nexus-staging-plugin', version: '0.11.0'
        classpath group: 'com.github.ben-manes', name: 'gradle-versions-plugin', version: '0.17.0'

    }
}

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.github.ben-manes.versions'

scmVersion {
    tag {
        prefix = 'chrome-reactive-kotlin'
    }
}

group = 'pl.wendigo'
version = scmVersion.version

jar {
    baseName = 'chrome-reactive-kotlin'
    version = scmVersion.version
}

configurations {
    ktlint
}

compileKotlin {
    kotlinOptions {
        languageVersion = "1.1"
        jvmTarget = "1.8"
        verbose = false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.10.0'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.12'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    ktlint group: 'com.github.shyiko', name: 'ktlint', version: '0.20.0'
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                description 'Chrome Reactive - low level, remote chrome debugger protocol client (DevTools)'
                url 'https://github.com/wendigo/chrome-reactive-kotlin'
                inceptionYear '2017'
                version version
                groupId group
                artifactId 'chrome-reactive-kotlin'
                packaging 'jar'

                scm {
                    connection 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
                    url 'https://github.com/wendigo/chrome-reactive-kotlin'
                    developerConnection 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
                }

                scm {
                    connection 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
                    url 'https://github.com/wendigo/chrome-reactive-kotlin'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Mateusz "Serafin" Gajewski'
                        email 'contact@serafin.tech'
                    }
                }
            }
        }
    }
}

artifacts {
    archives jar
}

nexusStaging {
    packageGroup = 'pl.wendigo'
    numberOfRetries = 3
    delayBetweenRetriesInMillis = 100000
    stagingProfileId = '5028463f095590'
    username = nexusUsername
    password = nexusPassword
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
    }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args = ["src/**/*.kt"]
}

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args = ["-F", "src/**/*.kt"]
}